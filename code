//Version 5.0

//PID, Servo, SPI, IntScheduler, Pixy, IRremote
#include <Servo.h>
#include <SPI.h>
#include <intScheduler.h>
#include <Pixy.h>
#include <IRremote.h>
#include <intPID.h>

//Declaring variables
int ServoxPin = 9;
int ServoyPin = 10;
int Ymid = 100;
int Xmid = 160;
uint16_t blocks;
int ServoXPositie;
int ServoYPositie;
int RECV_PIN = 7;

Pixy pixy;
Servo ServoX;
Servo ServoY;
intScheduler Restart(200);
intScheduler serial(1000);

#define BUTTON_1 0x202807F
#define BUTTON_3 0x202C03F
#define BUTTON_7 0x202E01F
#define BUTTON_9 0x202906F
#define BUTTON_5 0x202A05F
#define Left 0x20208F7
#define Right 0x2028877
#define Up 0x202D02F
#define Down 0x202708F
#define OK 0x202B04F
IRrecv irrecv(RECV_PIN);
decode_results results;

//PID variables
double Setpointx, Outputx;
double Setpointy, Outputy;
double Kp=3, Ki=2, Kd=2;
double Inputx;
double Inputy;

//Setting up PID systems for the x and y axis
intPID myPIDx(&Inputx, &Outputx, &Setpointx, Kp, Ki, Kd, DIRECT);
intPID myPIDy(&Inputy, &Outputy, &Setpointy, Kp, Ki, Kd, DIRECT);

//Setup
void setup() {
  Serial.begin(9600);
  pixy.init(); //Camera setup
  ServoX.attach(ServoxPin, 800, 2200);
  ServoY.attach(ServoyPin, 800, 2200);
  ServoX.writeMicroseconds(800);
  ServoY.writeMicroseconds(800);
  Setpointx = Xmid;
  Setpointy = Ymid;
  irrecv.enableIRIn();

  pinMode (RECV_PIN, INPUT);

  //set PID limits
  myPIDx.SetMode(AUTOMATIC);
  myPIDx.SetOutputLimits(-1000,1000);
  myPIDy.SetMode(AUTOMATIC);
  myPIDy.SetOutputLimits(-1000,1000);
}

void loop() {
   //Get the ball from the camera
   blocks = pixy.getBlocks();

  if (blocks) {
   //Assign ball coordinates to variables
   Inputx = pixy.blocks[0].x;
   Inputy = pixy.blocks[0].y;

   //Set wanted x and y coordinates (balance point)
   Setpointy = Ymid;
   Setpointx = Xmid;

   //enable PID
   myPIDx.SetMode(AUTOMATIC);
   myPIDx.SetOutputLimits(-1000,1000);
   myPIDy.SetMode(AUTOMATIC);
   myPIDy.SetOutputLimits(-1000,1000);

   //PID calculations
   myPIDx.Compute();
   myPIDy.Compute();
   
   //Translate the PID output to servo positions
   ServoXPositie = map(Outputx, -1000, 1000, 800, 2200);
   ServoYPositie = map(Outputy, 1000, -1000, 800, 2200);
  }
  
   //constrain the balancepoint to a specific area (If the balance point is too far to the edge then the ball can fall off more easely
   Xmid = constrain(Xmid, 100, 220);
   Ymid = constrain(Ymid, 40, 160);

   //Print the coordinates of the ball every second
   if (serial.Available()){
   Serial.print (Inputx);
   Serial.print (",");
   Serial.println (Inputy);
   }

   //If there are more than 0 objects, reset the timer
   if (blocks > 0){
      Restart.Synchronize();
   }

   //If there are no object for more than 1 second, reset the plate to a flat position
   if (Restart.Available()) {
      ServoXPositie = (2200 + 800) / 2;
      ServoYPositie = (2200 + 800) / 2;

      //disable PID
      myPIDx.SetMode(MANUAL);
      myPIDy.SetMode(MANUAL);
      myPIDx.ResetController();
      myPIDy.ResetController();
   }

   //Read infrared input and set balance point
   if (irrecv.decode(&results)){
    if (results.value == BUTTON_1){
      Serial.println ("BUTTON 1");
      Ymid = 60;
      Xmid = 120;
    }
    if (results.value == BUTTON_3){
      Serial.println ("BUTTON 3");
      Ymid = 60;
      Xmid = 200;
    }
    if (results.value == BUTTON_7){
      Serial.println ("BUTTON 7");
      Ymid = 140;
      Xmid = 120;
    }
    if (results.value == BUTTON_9){
      Serial.println ("BUTTON 9");
      Ymid = 140;
      Xmid = 200;
    }
    if (results.value == BUTTON_5){
      Serial.println ("BUTTON_5");
      Ymid = 100;
      Xmid = 160;
    }
    if (results.value == Left){
      Serial.println ("Left");
      Xmid = Xmid-20;
    }
    if (results.value == Right){
      Serial.println ("Right");
      Xmid = Xmid+20;
    }
    if (results.value == Up){
      Serial.println ("Up");
      Ymid = Ymid-20;
    }
    if (results.value == Down){
      Serial.println ("Down");
      Ymid = Ymid+20;
    }
    if (results.value == OK){
      Serial.println ("OK");
      Ymid = 100;
      Xmid = 160;
    }
    irrecv.resume();
   }

   //Write the servo positions to the servos
   ServoX.writeMicroseconds(ServoXPositie);
   ServoY.writeMicroseconds(ServoYPositie);
}
